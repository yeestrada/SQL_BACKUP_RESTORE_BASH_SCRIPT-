ECHO OFF
REM RESTORING CAN BE FROM REMOTE SERVER TO LOCAL SERVER ONLY

REM SET PATH TO SAVE THE FILES e.g. D:\backup, COULD BE SHARED FOLDER FOR REMOTE BACKUP
set BACKUPPATH=C:\SHAREDFOLDER
REM LOCAL TEMP DIRECTORY TO COPY BAK FILES TO REPLICATION 
set DEFAULTLOCALPATH=C:\

REM SET NAME OF THE SERVER AND INSTANCE FROM
set SERVERNAMEFROM=localhost
REM SET NAME OF THE SERVER AND INSTANCE TO
set SERVERNAMETO=localhost

REM SET LIST OF DATABASES
set list=DB1 DB2 DB3 DB4 DB5
REM SET NUMBER OF BACKUPS ALLOWED
set maxback=3

REM FROM AUTH DATA
set FROMUSER=sa
set FROMKEY=pwd

REM TO AUTH DATA
set TOUSER=sa
set TOKEY=pwd

REM ------------------CREATING AND DELETING FOLDERS--------------------

REM CREATE BACKUP GENERAL FOLDER
IF not exist %BACKUPPATH% (mkdir %BACKUPPATH%)

REM REMOVE OLDER BACKUPS
setlocal enableextensions enabledelayedexpansion
set /a maxback -= 1
set /a count = 0
(for /f %%D in ('dir %BACKUPPATH% /a:d /b /-N /o-n') do ( 
	Echo.%%D | findstr /C:"BACKUP">nul && (
		if !count! geq %maxback% ( 
			echo DELETING %BACKUPPATH%\%%D FOLDER
			rd /s /q %BACKUPPATH%\%%D
		) 		
		set /a count += 1
	)		
))
endlocal

REM GET TIMESTAMP FOR FOLDER NAME
For /f "tokens=2-4 delims=/ " %%a in ('date /t') do (set mydate=%%c%%b%%a)
For /f "tokens=1-2 delims=/:" %%a in ("%TIME%") do (set mytime=%%a%%b)
set DATESTAMP=%mydate%%mytime: =0%

REM DELETE TEMP_BACKUP FOLDER IF EXIST
IF EXIST %F% rd /s /q %BACKUPPATH%\BACKUP_%DATESTAMP%
REM CREATE TEMP FOLDER
MKDIR %BACKUPPATH%\BACKUP_%DATESTAMP%

REM EXIT /B n
REM ------------------CREATING NEW BACKUP--------------------
(for %%a in (%list%) do ( 
	echo CREATING BACKUP OF %%a DATABASE
	SQLCMD -S %SERVERNAMEFROM% -U %FROMUSER% -P %FROMKEY% -Q "BACKUP DATABASE %%a TO DISK = N'%BACKUPPATH%\BACKUP_%DATESTAMP%\%%a.bak' WITH INIT, NOUNLOAD, NOSKIP, NAME = N'Backup Automatico de %%a ', STATS = 10" -o %BACKUPPATH%\BACKUP_%DATESTAMP%\log.txt
))

REM EXIT /B n
REM ------------------RESTORING LATEST BACKUP--------------------

REM CREATE TEMP FOLDER 
MKDIR %DEFAULTLOCALPATH%BACKUP_%DATESTAMP%
ECHO COPYING BACKUP FILES TO LOCAL FOLDER %DEFAULTLOCALPATH%BACKUP_%DATESTAMP%
REM BRING BAK FILES TO LOCAL
XCOPY /s /i %BACKUPPATH%\BACKUP_%DATESTAMP% %DEFAULTLOCALPATH%BACKUP_%DATESTAMP% /E
REM RESTORE FROM LOCAL BAK FILES
(for %%a in (%list%) do ( 
	echo RESTORING %%a DATABASE
	SQLCMD -S %SERVERNAMETO% -U %TOUSER% -P %TOKEY% -Q "RESTORE DATABASE %%a FROM DISK='%DEFAULTLOCALPATH%BACKUP_%DATESTAMP%\%%a.bak'" -o %DEFAULTLOCALPATH%BACKUP_%DATESTAMP%\log.txt
))
REM ELIMINAR BACKUP TEMPORAL
rd /s /q %DEFAULTLOCALPATH%\BACKUP_%DATESTAMP%
